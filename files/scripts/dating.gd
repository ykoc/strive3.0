extends Node

var location
var person
var mood = 0.0 setget mood_set,mood_get
var fear = 0.0 setget fear_set
var stress = 0.0 setget stress_set
var date = false
var jail = false
var drunkness = 0.0
var actionhistory = []
var categories = ['Actions','P&P','Location','Items']
var locationarray = ['livingroom','town','dungeon','garden','bedroom']
var showntext = '' setget showtext_set,showtext_get
var turns = 0 setget turns_set,turns_get

var helpdescript = {
	mood = '[center]Mood[/center]\n A high mood increases loyalty and reduces stress after interaction is finished\nMood grows from positive interactions and decreases from negative interactions. Its also affected by loyalty.',
	fear = '[center]Fear[/center]\nFear increases obedience, however, it will also increase stress\nFear is raised through punishment and reduced through praise and is affected by Courage',
	stress = '[center]Stress[/center]\nStress accumulates from injury in combat,  poor treatment or unsanitary conditions\nHigh amounts of stress over a long period of time can reduce performance and loyalty',
	
	}

func fear_set(value):
	var difference = value - fear
	if difference > 0:
		var charcour = (person.cour/person.stats.cour_max)
		fear = max(0,value-difference*charcour/2)
	else:
		fear = max(0,value)
	if difference != 0:
		if difference > 0:
			$fear/Label.text = "+"
			$fear/Label.set("custom_colors/font_color", Color(1,0,0))
		else:
			$fear/Label.text = "—"
			$fear/Label.set('custom_colors/font_color', Color(0,1,0))
		$fear/Label/AnimationPlayer.play("fade")
	$fear.value = value*2

func stress_set(value):
	var difference = value - stress
	if difference != 0:
		if difference > 0:
			$stress/Label.text = "+"
			$stress/Label.set("custom_colors/font_color", Color(1,0,0))
		else:
			$stress/Label.text = "—"
			$stress/Label.set('custom_colors/font_color', Color(0,1,0))
		$stress/Label/AnimationPlayer.play("fade")
	person.stress = value
	$stress.value = person.stress
	stress = person.stress

func mood_set(value):
	var difference = value - mood
	if difference != 0:
		if difference > 0:
			$mood/Label.text = "+"
			$mood/Label.set("custom_colors/font_color", Color(0,1,0))
		else:
			$mood/Label.text = "—"
			$mood/Label.set('custom_colors/font_color', Color(1,0,0))
		$mood/Label/AnimationPlayer.play("fade")
	if difference > 0:
		mood = max(0,value + difference*(person.loyal/400))
	else:
		mood = max(0,value)
	$mood.value = value*2

func mood_get():
	return mood

var actionsdict = {
	chat = {
		group = 'Actions',
		name = 'Chat',
		reqs = 'true',
		descript = 'Have a friendly chat',
		effect = 'chat',
		disablereqs = "!person.traits.has('Mute')",
	},
	intimate = {
		group = "Actions",
		name = 'Intimate Talk',
		descript = 'Have an intimate talk',
		reqs = 'true',
		effect = 'intimate',
		disablereqs = "!person.traits.has('Mute')",
	},
	touch = {
		group = "Actions",
		name = 'Touch',
		reqs = 'true',
		descript = 'Light physical contact',
		effect = 'touch',
	},
	holdhands = {
		group = "Actions",
		name = 'Hold hands',
		descript = "Take $name's hand into yours",
		reqs = "location in ['garden','town','bedroom']",
		effect = 'holdhands',
	},
	combhair = {
		group = "Actions",
		name = 'Comb Hair',
		descript = "Comb $name's hair",
		reqs = "true",
		effect = 'combhair',
	},
	hug = {
		group = "Actions",
		name = 'Hug',
		descript = "Prolonged close physical contact",
		reqs = "true",
		effect = 'hug',
	},
	kiss = {
		group = "Actions",
		name = 'Kiss',
		descript = "Kiss $name lightly",
		reqs = "true",
		effect = 'kiss',
	},
	frenchkiss = {
		group = "Actions",
		name = 'French Kiss',
		descript = "Kiss $name in an erotic manner",
		reqs = "true",
		effect = 'frenchkiss',
	},
	pushdown = {
		group = "Actions",
		name = 'Push down',
		descript = "Force yourself on $name",
		reqs = "true",
		effect = 'pushdown',
	},
	proposal = {
		group = "Actions",
		name = 'Request intimacy',
		descript = "Ask $name if they would like to be intimate",
		reqs = "true",
		effect = 'propose',
	},
	praise = {
		group = "P&P",
		name = 'Praise',
		descript = "Praise $name for $his previous success to encourage further good behavior",
		reqs = "true",
		effect = 'praise',
	},
	pathead = {
		group = "P&P",
		name = 'Pat head',
		descript = "Praise $name and pat $his head for $his previous success to encourage further good behavior",
		reqs = "true",
		effect = 'pathead',
	},
	scold = {
		group = "P&P",
		name = 'Scold',
		descript = "Scold $name for $his previous mistakes to re-enforce obedience",
		reqs = "true",
		effect = 'scold',
	},
	slap = {
		group = "P&P",
		name = 'Slap',
		descript = "Slap $name across the face to reprimand $him",
		reqs = "true",
		effect = 'slap',
	},
	flagellate = {
		group = "P&P",
		name = 'Flagellate',
		descript = "Spank $name as punishment",
		reqs = "location == 'dungeon'",
		effect = 'flag',
	},
	whip = {
		group = "P&P",
		name = 'Whipping',
		descript = "Whip $name as punishment",
		reqs = "location == 'dungeon'",
		effect = 'whip',
	},
	wax = {
		group = "P&P",
		name = 'Hot Wax',
		descript = "Torture with hot wax",
		reqs = "location == 'dungeon'",
		effect = 'wax',
	},
	woodenhorse = {
		group = "P&P",
		name = 'Wooden Horse',
		descript = "Torture with a wooden horse",
		reqs = "location == 'dungeon'",
		effect = 'horse',
	},
	
	teach = {
		group = "Actions",
		name = 'Teach',
		descript = "Teach $name to accumulate learning points",
		reqs = "location in ['livingroom','garden']",
		effect = 'teach',
	},
	
	gift = {
		group = "Items",
		name = "Make Gift",
		descript = "Make a small decorative gift for $name. Requires 10 gold.",
		reqs = "!location == 'dungeon'",
		disablereqs = 'globals.resources.gold >= 10',
		effect = 'gift',
		onetime = true,
	},
	tea = {
		group = "Items",
		name = "Drink Tea",
		descript = "Serve tea for you and $name. Requires 1 supply.",
		reqs = 'location in ["livingroom","bedroom"]',
		disablereqs = 'globals.itemdict.supply.amount >= 1',
		effect = 'tea',
	},
	wine = {
		group = "Items",
		name = "Drink Wine",
		descript = "Serve wine for you and $name (Alcohol eases intimacy request but may cause a knockout). Requires 2 supplies",
		reqs = 'location in ["livingroom","bedroom","garden","town"]',
		disablereqs = 'globals.itemdict.supply.amount >= 2',
		effect = 'wine',
	},
	stop = {
		group = "Actions",
		name = "Stop",
		descript = "Leave $name right now",
		reqs = 'true',
		effect = 'stop',
	},
}
onready var nakedspritesdict = {
	Cali = {cons = 'calinakedhappy', rape = 'calinakedsad', clothcons = 'calineutral', clothrape = 'calisad'},
	Tisha = {cons = 'tishanakedhappy', rape = 'tishanakedneutral', clothcons = 'tishahappy', clothrape = 'tishaneutral'},
	Emily = {cons = 'emilynakedhappy', rape = 'emilynakedneutral', clothcons = 'emily2happy', clothrape = 'emily2worried'},
	Chloe = {cons = 'chloenakedhappy', rape = 'chloenakedneutral', clothcons = 'chloehappy2', clothrape = 'chloeneutral2'},
	Maple = {cons = 'fairynaked', rape = 'fairynaked', clothcons = 'fairy', clothrape = 'fairy'},
	Yris = {cons = 'yrisnormalnaked', rape = 'yrisshocknaked', clothcons = 'yrisnormal', clothrape = 'yrisshock'},
	Ayneris = {cons = 'aynerisneutralnaked', rape = 'aynerisangrynaked', clothcons = 'aynerisneutral', clothrape = 'aynerisangry'},
	Zoe = {cons = "zoehappynaked", rape = 'zoesadnaked', clothcons = 'zoehappy', clothrape = 'zoesad'},
	Melissa = {cons = "melissanakedfriendly", rape = 'melissanakedneutral', clothcons = 'melissafriendly', clothrape = 'melissaneutral'},
	}

var locationdicts = {
	livingroom = {code = 'livingroom',name = 'Living Room', background = 'mansion'},
	bedroom = {code = 'bedroom',name = 'Bedroom', background = 'mansion'},
	dungeon = {code = 'dungeon',name = 'Dungeon', background = 'jail'},
	garden = {code = 'garden',name = 'Garden', background = 'crossroads'},
	town = {code = 'town',name = 'Streets', background = 'localtown'},
}


var dateclassarray = []

class dateclass:
	var person
	var sex
	var name
	var lust = 0
	var lube = 0
	var sens = 0 

func _ready():
	for i in helpdescript:
		get_node(i).connect("mouse_entered",globals,'showtooltip',[helpdescript[i]])
		get_node(i).connect("mouse_exited",globals,'hidetooltip')

func initiate(tempperson):
	var text = ''
	self.visible = true
	self.mood = 0
	self.fear = 0
	self.drunkness = 0
	date = false
	$sexswitch.visible = false
	$end.visible = false
	$textfield/RichTextLabel.clear()
	
	actionhistory.clear()
	dateclassarray.clear()
	
	
	person = tempperson
	var newclass = dateclass.new()
	newclass.sex = person.sex
	newclass.name = person.name_short()
	newclass.person = globals.player
	dateclassarray.append(newclass)
	newclass = dateclass.new()
	newclass.person = person
	newclass.sex = person.sex
	newclass.name = person.name_short()
	dateclassarray.append(newclass)
	
	jail = person.sleep == 'jail'
	
	self.stress = person.stress
	self.turns = 10
	$fullbody.set_texture(null)
	if nakedspritesdict.has(person.unique):
		if person.obed >= 50 || person.stress < 10:
			$fullbody.set_texture(globals.spritedict[nakedspritesdict[person.unique].clothcons])
		else:
			$fullbody.set_texture(globals.spritedict[nakedspritesdict[person.unique].clothrape])
	elif person.imagefull != null && globals.loadimage(person.imagefull) != null:
		$fullbody.set_texture(globals.loadimage(person.imagefull))
	$textfield/slaveportrait.texture = null
	
	if person.imageportait != null && globals.loadimage(person.imageportait):
		$textfield/slaveportrait.set_texture(globals.loadimage(person.imageportait))
	else:
		person.imageportait = null
		$textfield/slaveportrait.set_texture(null)
	if $textfield/slaveportrait.texture == null:
		$textfield/slaveportrait/TextureRect.visible = false
	else:
		$textfield/slaveportrait/TextureRect.visible = true
	
	$textfield/masterportrait.texture = null
	if globals.player.imageportait != null && globals.loadimage(globals.player.imageportait):
		$textfield/masterportrait.set_texture(globals.loadimage(globals.player.imageportait))
	else:
		globals.player.imageportait = null
		$textfield/masterportrait.set_texture(null)
	if $textfield/masterportrait.texture == null:
		$textfield/masterportrait/TextureRect.visible = false
	else:
		$textfield/masterportrait/TextureRect.visible = true
	
	
	
	if jail == true:
		get_parent().background = 'jail'
		location = 'dungeon'
		text = "You visit [name2] in [his2] cell and decide to spend some time with [him2]. "
		$panel/categories/Location.disabled = true
	else:
		$panel/categories/Location.disabled = false
		get_parent().background = 'mansion'
		location = 'livingroom'
		text = "You meet [name2] and order [him2] to keep you company. "
		if person.loyal >= 25:
			text += "[he2] gladly accepts your order and is ready to follow you anywhere you take [him2]. "
			self.mood += 10
		elif person.obed >= 90:
			self.mood += 4
			text += "[he2] obediently agrees to your order and tries [his2] best to please you. "
		else:
			
			text += "Without great joy [he2] obeys your order and reluctantly joins you. "
		if person.lust >= 30:
			mood += 6
		elif person.traits.has("Devoted"):
			mood += 10
	
	self.showntext = text
	updatelist()
	$panel/categories/Actions.emit_signal("pressed")

var category


func showtext_set(value):
	var text = decoder(value)
	$textfield/RichTextLabel.bbcode_text = text
	showntext = text

func showtext_get():
	return $textfield/RichTextLabel.bbcode_text

func turns_set(value):
	turns = value
	$turns/Label.text = 'x'+str(value)
	if turns == 0:
		endencounter()

func turns_get(value):
	return turns

func selectcategory(button):
	for i in $panel/categories.get_children():
		i.pressed = false
		if i.name == button:
			i.pressed = true
	category = button
	updatelist()

func endencounter():
	var text = calculateresults()
	$end/RichTextLabel.bbcode_text = text
	$end.visible = true

func updatelist():
	for i in $panel/ScrollContainer/GridContainer.get_children():
		if i.name != 'Button':
			i.visible = false
			i.queue_free()
	$textfield/Label.text = locationdicts[location].name
	for i in actionsdict.values():
		if evaluate(i.reqs) == true && i.group == category:
			if i.has('onetime') && checkhistory(i.effect) > 0:
				continue
			var newnode = $panel/ScrollContainer/GridContainer/Button.duplicate()
			$panel/ScrollContainer/GridContainer.add_child(newnode)
			newnode.visible = true
			newnode.text = person.dictionary(i.name)
			newnode.connect("pressed",self,'doaction', [i.effect])
			newnode.connect("mouse_entered",self,'actiontooltip', [i.descript])
			newnode.connect("mouse_exited",globals,'hidetooltip')
			if i.has('disablereqs') && evaluate(i.disablereqs) == false:
				newnode.disabled = true
	if category == 'Location':
		for i in locationdicts.values():
			if i.code == location:
				continue
			var newnode = $panel/ScrollContainer/GridContainer/Button.duplicate()
			$panel/ScrollContainer/GridContainer.add_child(newnode)
			newnode.visible = true
			newnode.text = "Move to "+ i.name
			newnode.connect("pressed",self,'moveto', [i.code])
#			newnode.connect("mouse_entered",self,'actiontooltip', [i.descript])
#			newnode.connect("mouse_exited",globals,'hidetooltip')
	$panel/ScrollContainer/GridContainer.move_child($panel/ScrollContainer/GridContainer/Button, $panel/ScrollContainer/GridContainer.get_children().size())

func moveto(newloc):
	self.location = newloc
	if locationdicts[location].background != 'localtown':
		get_parent().background = locationdicts[location].background
	else:
		get_parent().background = globals.state.location
	yield(get_parent(),'animfinished')
	self.showntext = 'You lead [name2] to the [color=yellow]' + locationdicts[location].name + '[/color]. '
	if date == false && !newloc in ['bedroom','dungeon']:
		date = true
		self.mood += 5
		self.turns += 5
		self.showntext += "\n[color=green][name2] seems to be quite happy to be taken out of the usual place and ready to spend with you some additional time. [/color]"
	
	self.turns -= 1
	updatelist()

func actiontooltip(descript):
	globals.showtooltip(person.dictionary(descript))

func evaluate(input): #used to read strings as conditions when needed
	var script = GDScript.new()
	script.set_source_code("var person\nvar location\nfunc eval():\n\treturn " + input)
	script.reload()
	var obj = Reference.new()
	obj.set_script(script)
	obj.person = person
	obj.location = location
	return obj.eval()


func decoder(text):
	text = get_parent().get_node("interactions").decoder(text, [dateclassarray[0]], [dateclassarray[1]])
	return text

func doaction(action):
	self.showntext = decoder(call(action, person, checkhistory(action)))
	self.turns -= 1
	actionhistory.append(action)
	
	if turns%3:
		if location == 'bedroom':
			self.showntext += decoder("\n\n[color=yellow]Location influence:[/color] [name2] seems to be getting more into intimate mood...")
			person.lust += rand_range(2,3)
		elif location == 'garden' && person.conf < 50:
			self.mood += 3
			self.showntext += decoder("\n\n[color=yellow]Location influence:[/color] [name2] finds this place to be rather peaceful...")
		elif location == 'town' && person.conf >= 50:
			self.mood += 3
			self.showntext += decoder("\n\n[color=yellow]Location influence:[/color] [name2] finds this place to be rather joyful...")
		elif location == 'dungeon':
			fear += 3
			self.showntext += decoder("\n\n[color=yellow]Location influence:[/color] [name2] finds this place to be rather grim...")
	drunkness()
	updatelist()

func checkhistory(action):
	var counter = 0
	for i in actionhistory:
		if i == action:
			counter += 1
	return counter

func chat(person, counter):
	var text = ''
	text += "You attempt to initiate a friendly chat with [name2]. "
	
	if counter < 3 || randf() >= counter/10+0.1:
		text += "[name2] spends some time engaging in a friendly chat with you. "
		self.mood += 2
	else:
		self.mood -= 1
		text += "[name2] replies, but does so reluctantly. "
	
	
	return text

func intimate(person, counter):
	var text = ''
	text += "You talk to [name2] about personal matters. "
	
	if randf() >= counter/10 && self.mood >= 7 && person.loyal >= 10:
		text += "[he2] opens to you"
		self.mood += 3
		person.loyal += rand_range(2,5)
		if person.loyal >= 30 && randf() >= 0.65:
			text += ' and moves slightly closer'
			person.lust += 5
		text += '. '
	elif counter >= 5 && randf() >= 0.5:
		self.mood -= 1
		text += "[he2] looks at you contemplatively, but fails to make a connection. "
	else:
		self.mood -= 2
		text += "[he2] gives you an awkward look. "
	
	
	return text

func touch(person, counter):
	var text = ''
	text += "You casually touch [name2] in various places. "
	
	if counter < 3 && person.obed >= 80:
		text += "[he2] reacts relaxingly to your touch"
		self.mood += 2
		if person.loyal >= 10 && randf() >= 0.65:
			text += ' and touches you back'
			person.lust += 3
		text += '. '
	else:
		self.mood -= 1
		text += "[he2] reacts coldly to your touch. "
	return text

func holdhands(person, counter):
	var text = ''
	if location != 'bedroom':
		text += "You take [name2]'s hand into yours and stroll around. "
	else:
		text += "You take [name2]'s hand into yours and move closer. "
	if (counter < 3 || randf() >= 0.4) && self.mood >= 4:
		text += "[he2] holds your hand firmly. "
		self.mood += 2
		person.loyal += rand_range(2,3)
	else:
		self.mood -= 1
		text += "[he2] holds your hand, but looks reclusive. "
	
	return text

func combhair(person, counter): #play with hair would make more sense?
	var text = ''
	text += "You gently comb [name2]'s hair. "
	
	if (counter < 3 || randf() >= 0.8) && self.mood >= 4:
		text += "[he2] smiles and looks pleased. "
		self.mood += 2
		person.loyal += rand_range(2,3)
	else:
		self.mood -= 1
		text += "[he2] looks uncomfortable. "
	
	return text


func hug(person, counter): 
	var text = ''
	text += "You embrace [name2] in your arms. "
	
	if (counter < 3 || randf() >= 0.7) && self.mood >= 6:
		text += "[he2] embraces you back resting [his2] head on your chest. "
		self.mood += 3
		person.lust += 3
		person.loyal += rand_range(2,3)
	else:
		self.mood -= 2
		text += "[he2] does not do anything waiting uncomfortably for you to finish. "
	
	return text

func kiss(person, counter): 
	var text = ''
	text += "You gently kiss [name2] on the cheek. "
	
	if (self.mood >= 4 || person.loyal >= 15):
		text += "[he2] blushes and looks away. "
		self.mood += 3
		person.lust += 1
	else:
		self.mood -= 2
		text += "[he2] abruptly stops you, showing [his2] disinterest. "
	
	return text

func frenchkiss(person, counter): 
	var text = ''
	text += "You invade [name2]'s mouth with your tongue. "
	
	if (self.mood >= 10 && person.lust >= 20) || person.loyal >= 25:
		text += "[he2] closes eyes passionately accepting your kiss. "
		if !person.traits.has("Bisexual") && !person.traits.has("Homosexual") && person.sex == globals.player.sex:
			self.mood += 1
			person.lust += 1
		else:
			self.mood += 3
			person.lust += 3
	else:
		self.mood -= 4
		text += "[he2] abruptly stops you, showing [his2] disinterest. "
	
	return text

func pushdown(person, counter):
	var text = ''
	var mode
	text += "You forcefully push [name2] down giving [him2] a sultry look. "
	
	if self.mood*4 + person.loyal + person.lust >= 100 || (person.traits.has("Likes it rough") && self.mood*5 + person.loyal + person.lust >= 75):
		text += "[he2] closes eyes and silently accepts you. "
		self.mood += 3
		person.lust += 3
		mode = 'rapeconsent'
	else:
		self.mood -= 6
		text += "[he2] resists and pushes you back. "
		mode = 'abuse'
	showsexswitch(text,mode)
	return text

func propose(person, counter):
	var text = ''
	var mode
	if person.consent == true:
		text = "[name2] previously gave you [his2] consent and you proceed with your intentions. "
		mode = 'sex'
		globals.state.sexactions += 1
		showsexswitch(text, mode)
		return text
	else:
		text += "You ask [name2] if [he2] would like to take your relationship to the next level. "
		var difficulty =  self.mood*5 + person.loyal*2 + person.lust + drunkness*3
		if person.sex == globals.player.sex && !person.traits.has('Homosexual') && !person.traits.has("Bisexual"):
			difficulty -= 10
		if str(person.relatives.father) == '0' || str(person.relatives.mother) == '0':
			difficulty -= 10
		for i in person.traits.has('Prude'):
			difficulty -= 5
		if difficulty <= 100:
			text += "[he2] shows a troubled face and rejects your proposal. "
			self.mood -= 4
			return text
		else:
			person.lust += 3
			mode = 'sex'
			globals.state.sexactions += 1
			showsexswitch(text, mode)
			text += "[he2] gives a meek nod and you lead [him2] to bedroom."
			text += "\n\n[color=green]Unlocked sexual actions with [name2].[/color]"
			if person.levelupreqs.has('code') && person.levelupreqs.code == 'relationship':
				text += "\n\n[color=green]After getting closer with [name2], you felt like [he2] unlocked new potential. [/color]"
				person.levelup()
			person.consent = true
			
			return text
	
	

var sexmode

func showsexswitch(text, mode):
	$sexswitch.visible = true
	sexmode = mode
	$end/RichTextLabel.bbcode_text = text
	if mode == 'abuse':
		text += "\n[color=yellow]Rape [name2] anyway?[/color]"
		$sexswitch/confirmsex.visible = true
		$sexswitch/cancelsex.visible = true
	elif mode == 'rapeconsent':
		sexmode = 'sex'
		$sexswitch/confirmsex.visible = true
		$sexswitch/cancelsex.visible = false
	elif mode == 'sex':
		$sexswitch/confirmsex.visible = true
		$sexswitch/cancelsex.visible = false
	
	text = decoder(text) 
	if $sexswitch/cancelsex.visible == false:
		text += calculateresults()
	$sexswitch/RichTextLabel.bbcode_text = text

func praise(person, counter):
	var text = ''
	text += "You praise [name2] for [his2] recent behavoir. "
	
	if person.obed >= 85 && person.praise == 0 && counter < 2:
		self.mood += 3
		self.fear -= 1
		if person.race == 'Human':
			person.praise = 4
		else:
			person.praise = 2
		self.stress -= rand_range(5,10)
		text = text + "[he2] listens to your praise with joy evident on [his2] face.  "
	elif person.obed >= 85:
		text = text + "[he2] takes your words respectfully but without great joy. You’ve probably overpraised [him2] lately. "
		person.praise += 1
		
	else:
		text = text + "[he2] takes your praise arrogantly, gaining joy from it. "
		if person.race == 'Human':
			person.praise = 2
		else:
			person.praise = 1
		self.mood += 3
		self.fear -= 3
		person.loyal -= rand_range(1,2)
	
	return text

func pathead(person, counter):
	var text = ''
	text += "You pat [name2]'s head and praise [him2] for [his2] recent behavior. "
	
	if counter < 5 || randf() >= 0.4:
		self.mood += 2
		self.fear -= 2
		text = text + "[he2] takes it with joy evident on [his2] face.  "
	else:
		text = text + "[he2] seems to be bored from repeated action. "
		self.mood -= 1
	return text

func scold(person, counter):
	var text = ''
	text += "You scold [name2] for [his2] recent faults. "
	if person.obed < 85 && person.punish.expect == false:
		if person.effects.has('captured') == true:
			person.effects.captured.duration -= 1
		text = text + "\n[he2] seems to be taking your reprimand seriously."
		person.punish.expect = true
		if person.race == 'Human':
			person.punish.strength = 10 - person.cour/25
		else:
			person.punish.strength = 5 - person.cour/25
		self.mood -= 2
		self.fear += 2
	elif person.obed >= 85:
		text = text + '\n[he2] unhappy to your reprimand, as [he2] does not believe [he2] has offended you.'
		self.mood -= 5
		self.fear += 2
	else:
		text = text + "\n[he2] does not seem very afraid of your threats, as you haven't followed through on them previously."
		self.mood -= 3
		self.fear += 1
		person.punish.expect = true
		if person.race == 'Human':
			person.punish.strength = 10 - person.cour/25
		else:
			person.punish.strength = 5 - person.cour/25
		if person.effects.has('captured') == true:
			person.effects.captured.duration += 1
	
	return text


func punishaddedeffect():
	var text = ''
	if person.obed < 75 || person.traits.has('Masochist') == true:
		if person.effects.has('captured') == true:
			person.effects.captured.duration -= 1
			self.stress += rand_range(8,14)
			self.mood -= 3
			text = text + "\n[he2] glares at you with sorrow and hatred, showing leftovers of a yet untamed spirit."
		else:
			text = text + "\n[he2] begs for mercy and takes your lesson to heart."
			self.stress += rand_range(4,8)
			self.fear += 1
		if person.punish.expect == true:
			self.mood += 1
			self.fear += 1
			if person.race == 'Human':
				person.punish.strength += 10 - person.cour/25
			else:
				person.punish.strength += 5 - person.cour/25
		else:
			self.stress += rand_range(3,6)
			self.mood -= 2
			self.fear += 4
	else:
		text = text + "\n[he2] obediently takes [his2] punishment and begs for your forgiveness, but [name2] doesn't feel like [he2] truly deserves such a treatment."
		self.mood -= 2
		self.fear += 2
		self.stress += rand_range(5,10)
	if person.traits.has("Masochist"):
		person.lust += rand_range(2,4)
		self.mood += 3
	return text

func slap(person, counter):
	var text = ''
	text += "You slap [name2] across the face as punishment. [his2] cheek gets red. "
	self.fear += 2
	self.mood -= 2
	text += punishaddedeffect()
	return text

func flag(person, counter):
	var text = ''
	text += "You put [name2] on the punishment table, and after exposing [his2] rear, punish it with force. "
	
	self.fear += 3
	self.mood -= 3
	
	text += punishaddedeffect()
	return text

func whip(person, counter):
	var text = ''
	text += "You put [name2] on the punishment table, and after exposing [his2] rear, whip it with force. "
	
	self.fear += 4
	self.mood -= 3
	
	text += punishaddedeffect()
	return text

func horse(person, counter):
	var text = ''
	text += "You tie [name2] securely to the wooden horse with [his2] legs spread wide. [he2] cries with pain under [his2] own weight. "
	
	self.fear += 4
	self.mood -= 4
	person.lust += rand_range(5,10)
	
	text += punishaddedeffect()
	
	return text

func wax(person, counter):
	var text = ''
	text += "You put [name2] on the punishment table and after exposing [his2] body you drip hot wax over it making [him2] cry with pain. "
	
	self.fear += 2
	self.mood -= 3
	
	text += punishaddedeffect()
	
	return text

func teach(person, counter):
	var text = ''
	var value = round(3 + person.wit/12) - drunkness*2
	if person.traits.has("Clever"):
		value += value*0.25
	text += "You spend some time with [name2], teaching [him2]. "
	
	if mood >= 5 || counter < 4:
		person.learningpoints += value
		
		mood -= 1
		text += "[name2] learns new things under your watch. " 
		if person.traits.has("Clever"):
			text += "\n[color=green]Trait bonus: Clever[/color]"
	else:
		text += "[name2] looks heavily bored,not taking the lesson seriously. " 
	
	if drunkness > 0:
		text += "\nLesson was less effective due to [name2]'s alcohol intoxication. "
	
	return text

func gift(person, counter):
	var text = ''
	text += "You present [name2] with a small decorative gift. "
	
	if person.obed >= 75:
		self.mood += 5
		self.fear -= 4
		if person.race == 'Human':
			person.praise = 6
		else:
			person.praise = 3
		text = text + "[he2] accepts your gift with a pleasant smile on [his2] face.  "
		
	else:
		text = text + "[he2] takes your gift arrogantly, barely respecting your intention. "
		if person.race == 'Human':
			person.praise = 2
		else:
			person.praise = 1
		self.mood += 3
		self.fear -= 3
	
	
	globals.resources.gold -= 10
	
	return text


func tea(person, counter):
	var text = ''
	text += "You serve tea for you and [name2]. While drinking, you both chatand get a bit closer.  "
	
	if counter <= 3 || randf() >= 0.5:
		self.mood += 5
		self.stress -= rand_range(2,5)
		text += "[name2] seems to be pleased with your generosity and enjoys your company. "
	else:
		self.mood += 1
	
	globals.itemdict.supply.amount -= 1
	
	return text

func wine(person, counter):
	var text = ''
	text += "You serve fresh wine for you and [name2]. "
	
	if self.mood < 5 || person.obed < 80:
		text = "[he2] refuses to drink with you. "
	else:
		if counter < 3:
			text += "[he2] drinks with you and [his2] mood seems to improve."
			self.mood += 4
			self.stress -= rand_range(5,10)
			drunkness += 1
		else:
			self.mood += 2
			text += "[he2] keeps you company, but the wine does not seem to affect [him2] as heavily as before. "
			drunkness += 1
		
		globals.itemdict.supply.amount -= 2
	
	if person.traits.has("Alcohol Intolerance"):
		drunkness += 1
	
	return text

func stop(person, counter):
	var text = ''
	turns = 1
	return text

func drunkness():
	if drunkness > person.send*2 + 1:
		person.away.duration = 1
		endencounter()
		$end/RichTextLabel.bbcode_text += decoder('\n\n[color=yellow][name2] has passed out from alcohol overdose. [/color]')

func calculateresults():
	var text = ''
	var obed = 0
	var stress = 0
	var loyal = 0
	var charcour = (person.cour/person.stats.cour_max)*100
	var charconf = (person.conf/person.stats.conf_max)*100
	obed = fear*1.2
	stress = max((fear-5)*2,0) - mood/4
	loyal = 3+(mood/10-fear/1.75)
	text += "\nFinal results: "
	text += "\nObedience: " + str(floor(obed)) + "\nLoyalty: " + str(floor(loyal)) + "\nStress: " + str(floor(stress))
	var dict = {obed = obed, stress = stress, loyal = loyal}
	
	for i in dict:
		person[i] += dict[i]
	return text


func _on_finishbutton_pressed():
	get_parent()._on_mansion_pressed()
	yield(get_parent(),'animfinished')
	self.visible = false

func _on_cancelsex_pressed():
	$sexswitch.visible = false

func _on_confirmsex_pressed():
	calculateresults()
	self.visible = false
	get_parent().sexmode = 'sexmode'
	get_parent().sexslaves = [person]
	get_parent()._on_startbutton_pressed()



